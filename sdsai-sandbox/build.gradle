plugins {
  id 'java-library'
  // Sonatype's Nexus doensn't support the stronger cryptographic hashes.
  // id 'maven-publish'
  id 'maven'
  id 'signing'
  id 'eclipse'
  id 'net.researchgate.release' version '2.6.0'
}

release {
  tagTemplate = '${name}-${version}'
}  

dependencies {
  implementation project(':sdsai-common')
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.slf4j:slf4j-simple:1.7.21'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {

    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: releaseRepository) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: snapshotRepository) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name "SDSAI Sandbox"
                    description 'Simple datastructures and algorithms.'
                    url 'https://github.com/basking2/sdsai'
                    packaging 'jar'
                    // optionally artifactId can be defined here

                    scm {
                        connection 'scm:git:git@github.com:basking2/sdsai.git'
                        developerConnection 'git@github.com:basking2/sdsai.git'
                        url 'https://github.com/basking2/sdsai.git'
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id 'basking2'
                            name 'Sam'
                            email 'basking2@yahoo.com'
                        }
                    }
                }
            }
        }
    }
}

