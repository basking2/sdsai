plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

release {
    tagTemplate = '${name}-${version}'
    git {
        requireBranch.set('main')
    }
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.21'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.slf4j:slf4j-simple:1.7.21'
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

java {
    withJavadocJar()
    withSourcesJar()
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {

    publishing {
        repositories {
            maven {
                authentication {
                    basic(BasicAuthentication)
                }
                credentials(PasswordCredentials) {
                    username = ossrhUsername
                    password = ossrhPassword
                }
                url = version.endsWith('SNAPSHOT') ? snapshotRepository : releaseRepository
            }
        }
        publications {
            mavenJava(MavenPublication) {
                from components.java
                pom {
                    name = "SDSAI Net"
                    description = 'Simple datastructures and algorithms.'
                    url = 'https://github.com/basking2/sdsai'
                    packaging = 'jar'
                    scm {
                        connection = 'scm:git:git@github.com:basking2/sdsai.git'
                        developerConnection = 'git@github.com:basking2/sdsai.git'
                        url = 'https://github.com/basking2/sdsai.git'
                    }
                    licenses {
                        license {
                            name = 'MIT'
                            url = 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id = 'basking2'
                            name = 'Sam'
                            email = 'basking2@yahoo.com'
                        }
                    }
                }
            }
        }
    }

    signing {
        sign publishing.publications.mavenJava
    }


    javadoc {
        if(JavaVersion.current().isJava9Compatible()) {
            options.addBooleanOption('html5', true)
        }
    }
}

