plugins {
  id 'java'
  // Sonatype's Nexus doensn't support the stronger cryptographic hashes.
  // id 'maven-publish'
  id 'maven'
  id 'signing'
  id 'application'
}

mainClassName = 'com.github.basking2.sdsai.itrex.Shell'
applicationDefaultJvmArgs = []
applicationName = 'itrex'

run {
    standardInput = System.in
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

repositories {
}

processResources {
    expand projectVersion: project.version,
            projectName: project.name
}

dependencies {
    implementation project(':sdsai-itrex')
    implementation 'commons-cli:commons-cli:1.4'
    implementation 'org.jline:jline:3.4.0'
    implementation 'org.slf4j:slf4j-simple:1.7.21'
    testImplementation 'junit:junit:4.12'
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: releaseRepository) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: snapshotRepository) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name "SDSAI Itrex Shell"
                    description 'Simple datastructures and algorithms.'
                    url 'https://github.com/basking2/sdsai'
                    packaging 'jar'
                    // optionally artifactId can be defined here

                    scm {
                        connection 'scm:git:git@github.com:basking2/sdsai.git'
                        developerConnection 'git@github.com:basking2/sdsai.git'
                        url 'https://github.com/basking2/sdsai.git'
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id 'basking2'
                            name 'Sam'
                            email 'basking2@yahoo.com'
                        }
                    }
                }
            }
        }
    }
}


task runShell(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = 'com.github.basking2.sdsai.itrex.Shell'

    standardInput = System.in
    // arguments to pass to the application
    //args ''
}
