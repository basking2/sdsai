plugins {
  id 'java'
  id 'maven-publish'
  id 'signing'
  id 'application'
}

mainClassName = 'com.github.basking2.sdsai.itrex.Shell'
applicationDefaultJvmArgs = []
applicationName = 'itrex'

run {
    standardInput = System.in
}

compileJava {
    options.compilerArgs << '-Xlint:unchecked'
}

repositories {
}

processResources {
    expand projectVersion: project.version,
            projectName: project.name
}

dependencies {
    implementation project(':sdsai-itrex')
    implementation 'commons-cli:commons-cli:1.4'
    implementation 'org.jline:jline:3.4.0'
    implementation 'org.slf4j:slf4j-simple:1.7.21'
    testImplementation 'junit:junit:4.12'
}


repositories {
    mavenCentral()
    jcenter()
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
  publishing {
    repositories {
      maven {
        url = version.endsWith('SNAPSHOT') ? snapshotRepository : releaseRepository
        credentials {
          username ossrhUsername
          password ossrhPassword
        }
      }
    }

    publications {

      mavenJava(MavenPublication) {
        groupId = group
        artifactId = 'sdsai-itrex-shell'

        pom {
	  name = "SDSAI Itrex Shell"
          description = 'Simple datastructures and algorithms.'
          url = 'https://github.com/basking2/sdsai'
          scm {
            connection = 'scm:git:git@github.com:basking2/sdsai.git'
            developerConnection = 'git@github.com:basking2/sdsai.git'
            url = 'https://github.com/basking2/sdsai.git'
          }
          licenses {
            license {
              name = "MIT"
              url = 'https://opensource.org/licenses/MIT'
            }
          }

          developers {
            developer {
              id = 'basking2'
              name = 'Sam'
              email = 'basking2@yahoo.com'
            }
          }
        }
      }
    }
  }

  signing {
    sign publishing.publications.mavenJava
  }
}


task runShell(type: JavaExec) {
    classpath = sourceSets.main.runtimeClasspath

    main = 'com.github.basking2.sdsai.itrex.Shell'

    standardInput = System.in
    // arguments to pass to the application
    //args ''
}
