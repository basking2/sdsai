
plugins {
  id 'java-library'
  // Sonatype's Nexus doensn't support the stronger cryptographic hashes.
  // id 'maven-publish'
  id 'maven'
  id 'signing'
  id "org.asciidoctor.convert" version "2.3.0"
  id 'net.researchgate.release' version '2.8.1'
}

compileJava {
	options.compilerArgs << '-Xlint:unchecked'
}

compileTestJava {
    options.compilerArgs << '-Xlint:unchecked'
}

processResources {
    expand projectVersion: project.version,
           projectName: project.name
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.21'
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.slf4j:slf4j-simple:1.7.21'
}

release {
  tagTemplate = '${name}-${version}'
  git {
    requireBranch = 'main'
  }
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('ossrhUsername') && project.hasProperty('ossrhPassword')) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: releaseRepository) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: snapshotRepository) {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                pom.project {
                    name 'SDSAI Itrex'
                    // optionally artifactId can be defined here
                    description 'Simple datastructures and algorithms.'
                    packaging 'jar'
                    url 'https://github.com/basking2/sdsai'

                    scm {
                        connection 'scm:git:git@github.com:basking2/sdsai.git'
                        developerConnection 'git@github.com:basking2/sdsai.git'
                        url 'https://github.com/basking2/sdsai.git'
                    }

                    licenses {
                        license {
                            name 'MIT'
                            url 'https://opensource.org/licenses/MIT'
                        }
                    }

                    developers {
                        developer {
                            id 'basking2'
                            name 'Sam'
                            email 'basking2@yahoo.com'
                        }
                    }
                }
            }
        }
    }
}

